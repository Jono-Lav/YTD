# all docs were generated by chatgpt, i hate writing docs
## Base class for any interactable object in the world.
##
## Handles:
## - Interaction prompts
## - Locked / unlocked state
## - One-time interactions
## - Input key display
##
## Interaction state is tracked globally using `InteractableTracker`,
## with a UNIQUE ID derived automatically from this node's scene path.
##
## ⚠️ IMPORTANT:
## Since the ID is based on `get_path()`, moving or renaming this node
## in the scene tree will change its ID and reset its tracked state.
##
## Best used for:
## - Doors
## - Levers
## - Pickups
## - Terminals
class_name Interactable
extends Node


## Text shown when the object can be interacted with.
@export var prompt: String = "Interact"

## Text shown when the object is locked.
@export var locked_prompt: String = "Locked"

## Input action used to interact (must exist in InputMap).
@export var prompt_input: String = "interact"

## If true, the object starts locked and must be unlocked via code.
@export var lock: bool = false

## If true, this object can only be interacted with once.
## Uses `InteractableTracker` to remember interaction state.
@export var once: bool = false

## If true, allows displaying a custom locked message.
## (Optional behavior for UI systems.)
@export var show_locked_message: bool = false

## Optional message shown when interaction is blocked by a lock.
@export var locked_message: String = ""


## Unique ID for this interactable.
## Automatically derived from the node's scene path.
@onready var id: String = get_path()
@onready var interact_ray : RichTextLabel = get_node("/root/" + get_tree().current_scene.name + "/player/head/interact_ray/prompt")

## Initializes lock state when the node enters the scene tree.
func _notification(what) -> void:
	if what == NOTIFICATION_READY:
		# Initialize tracker state
		if lock:
			InteractableTracker.lock_interactable(id)
		else:
			InteractableTracker.unlock_interactable(id)


## Returns the prompt shown to the player.
## Automatically hides the prompt if the object was already used (once=true).
func get_prompt() -> String:
	if once and InteractableTracker.has_interacted(id):
		return ""

	var key_name := ""
	for action in InputMap.action_get_events(prompt_input):
		if action is InputEventKey:
			key_name = action.as_text_physical_keycode()
			break

	if InteractableTracker.is_locked(id):
		return locked_prompt

	return prompt + "\n[" + key_name + "]"


## Called when the player successfully interacts with the object.
##
## Intended to be overridden by subclasses.
func interact(body):
	if once and InteractableTracker.has_interacted(id):
		return

	if once:
		InteractableTracker.mark_interacted(id)

## Called when the player attempts to interact while the object is locked.
##
## Intended to be overridden for custom behavior.
func locked(body):
	print(body.name, " tried to interact with locked ", name)

## Unlocks this interactable globally.
func unlock():
	InteractableTracker.unlock_interactable(id)

## Locks this interactable globally.
func lockObj():
	InteractableTracker.lock_interactable(id)
