# all docs were generated by chatgpt, i hate writing docs
## Base NPC class that handles interaction prompts, dialogue, animation,
## optional name tags, and player-facing rotation.
##
## ⚠️ IMPORTANT – `_process` OVERRIDE RULE:
## This class uses `_process()` internally to update the NPC name tag.
##
## If you extend this class AND override `_process(delta)`:
## - You MUST call `super(delta)` as the FIRST line
##   OR
## - Disable the name tag entirely (`show_nametag = false`)
##
## Failing to do this will cause the name tag to stop updating or break.
##
## Example:
## func _process(delta):
##     super(delta) # REQUIRED if show_nametag == true
##     # your code here
class_name Npc
extends Node

## Text shown when the player can interact with the NPC.
## You can use `[name]` as a placeholder for the NPC's name.
@export var prompt_message: String = "TALK"

## Input action used for interaction (must exist in InputMap).
@export var prompt_input: String = "interact"

## Dialogue resource played when interacting with this NPC.
@export var dialogue: DialogueResource

## If true, the NPC turns to face the player when dialogue starts.
@export var turn_talk: bool = false

## If true, the NPC turns AWAY from the player instead of toward them.
## Only relevant if `turn_talk` is enabled.
@export var turn_opposite: bool = false

## If true, the player is NOT frozen during dialogue.
## Useful for ambient or background conversations.
@export var free_talk: bool = false

## If true, this NPC can only be interacted with once.
## Uses `NpcTracker` to remember interactions.
@export var play_once: bool = false

## Shows a floating name tag above the NPC.
##
## ⚠️ If enabled, this class relies on `_process()` to keep the
## name tag positioned correctly.
##
## If you override `_process()`, you MUST call `super(delta)`
## or disable this option.
@export var show_nametag: bool = false : set = set_show_nametag


## Reference to the player node (expected to be named "player"
## in the current scene root).
@onready var player: CharacterBody3D = get_node(
	"/root/" + get_tree().current_scene.name + "/player"
)

## Unique ID used for tracking interactions.
@onready var id: String = get_path()


## Collision shape used to disable player collision during dialogue.
var collision: CollisionShape3D

## AnimationPlayer used for idle/talk animations.
var anim: AnimationPlayer

## Name of the idle animation (auto-detected).
var idle_anim: String = ""

## Name of the talking animation (auto-detected).
var talk_anim: String = ""

## Display name of the NPC.
## Defaults to the parent node name if not set.
var npc_name: String = ""

## 3D label shown above the NPC when `show_nametag` is enabled.
var nametag: Label3D


## Dummy initializer to ensure `init_npc()` runs once the node is ready.
@onready var _init_dummy: int = init_npc()


## Initializes NPC components:
## - Detects animations and collision
## - Sets up idle/talk animations
## - Creates and attaches name tag
func init_npc() -> int:
	if npc_name.is_empty():
		npc_name = (
			get_parent().name
			if not get_parent() == get_tree().current_scene
			or get_parent().get_child_count() > 1
			else name
		)

	collision = find(self, "CollisionShape3D")
	anim = find(self, "AnimationPlayer")

	if anim:
		for anim_name in anim.get_animation_list():
			if "idle" in anim_name.to_lower():
				idle_anim = anim_name
				anim.get_animation(idle_anim).loop = true
				anim.play(idle_anim)
			elif "talk" in anim_name.to_lower():
				talk_anim = anim_name
				anim.get_animation(talk_anim).loop = true

	nametag = Label3D.new()
	nametag.text = npc_name
	nametag.billboard = BaseMaterial3D.BILLBOARD_FIXED_Y
	nametag.pixel_size = 0.006
	nametag.visible = show_nametag
	get_parent().add_child(nametag)

	set_process(true)
	return 1


## Returns a point above the NPC used for positioning the name tag.
func get_highest_point() -> Vector3:
	var base_pos = get_parent().global_position
	return base_pos + Vector3(0, 2.0, 0)


## Updates the name tag position to follow the NPC.
func update_nametag_position():
	if not nametag:
		return
	if not get_parent().is_inside_tree():
		return
	if not nametag.is_inside_tree():
		if nametag.get_parent() != get_parent():
			get_parent().add_child(nametag)
		return

	var highest_point = get_highest_point()
	nametag.global_position = highest_point + Vector3(0, 0.3, 0)


## Internal process loop.
##
## ⚠️ If overridden in a subclass, `super(delta)` MUST be called
## if `show_nametag` is enabled.
func _process(delta: float) -> void:
	if nametag:
		nametag.visible = show_nametag
	if show_nametag:
		update_nametag_position()


## Setter for enabling/disabling the name tag safely.
func set_show_nametag(value: bool) -> void:
	show_nametag = value
	if nametag and nametag.is_inside_tree():
		nametag.visible = value
	if value:
		update_nametag_position()


## Recursively finds the first child of a given class name.
func find(parent: Node, type: String) -> Variant:
	for child in parent.get_children():
		if child.get_class() == type:
			return child
		if child.get_child_count() > 0:
			var found = find(child, type)
			if found:
				return found
	return null


## Builds the interaction prompt shown to the player.
func get_prompt():
	if play_once and NpcTracker.has_interacted(id):
		return ""

	var key_name = ""
	for action in InputMap.action_get_events(prompt_input):
		if action is InputEventKey:
			key_name = action.as_text_physical_keycode()
			break

	if "[name]" in prompt_message:
		if not npc_name.is_empty():
			prompt_message = prompt_message.replace("[name]", npc_name)
		else:
			prompt_message = "Interact"

	return prompt_message + "\n[" + key_name + "]"


## Starts dialogue interaction with this NPC.
func dialog():
	if play_once and NpcTracker.has_interacted(id):
		return

	if dialogue:
		Global.canJump = false
		Global.playerStop = false if free_talk else true

		if turn_talk:
			var direction = player.global_transform.origin - get_parent().global_transform.origin
			direction.y = 0
			var direc = direction if not turn_opposite else -direction
			var new_transform = Transform3D.IDENTITY.looking_at(direc, Vector3.UP)
			var current_scale = get_parent().scale
			get_parent().global_transform = Transform3D(new_transform.basis, get_parent().global_transform.origin)
			get_parent().scale = current_scale

		if collision:
			collision.disabled = true

		if talk_anim:
			anim.play("talk")

		DialogueManager.show_example_dialogue_balloon(dialogue)
		await DialogueManager.dialogue_ended

		if collision:
			collision.disabled = false

		Global.playerStop = false if !free_talk else Global.playerStop
		Global.canJump = true

		if anim:
			anim.play(idle_anim if idle_anim else "RESET")

		if play_once:
			NpcTracker.mark_interacted(id)
	else:
		print("No dialogue resource assigned to ", name)


## Called when a body interacts with this NPC.
func interact(body):
	print(body.name, " interacted with ", name)
